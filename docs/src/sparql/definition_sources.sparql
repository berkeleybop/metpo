PREFIX metpo: <https://w3id.org/metpo/>
PREFIX IAO: <http://purl.obolibrary.org/obo/IAO_>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

SELECT ?metpo_id ?label ?definition ?definition_source
WHERE {
    ?cls a owl:Class .
    FILTER(STRSTARTS(STR(?cls), "https://w3id.org/metpo/"))
    
    BIND(REPLACE(STR(?cls), "https://w3id.org/metpo/", "METPO:") AS ?metpo_id)
    
    ?cls rdfs:label ?label .
    OPTIONAL { ?cls IAO:0000115 ?definition }
    
    # Find axiom annotations for definition sources
    OPTIONAL {
        ?axiom a owl:Axiom .
        ?axiom owl:annotatedSource ?cls .
        ?axiom owl:annotatedProperty IAO:0000115 .
        ?axiom IAO:0000119 ?definition_source .
    }
}
ORDER BY ?metpo_id ?definition_source
